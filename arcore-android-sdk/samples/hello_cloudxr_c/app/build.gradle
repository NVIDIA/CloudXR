apply plugin: 'com.android.application'

/*
The arcore aar library contains the native shared libraries.  These are
extracted before building to a temporary directory.
 */
def arcore_libpath = "${buildDir}/arcore-native"

// Create a configuration to mark which aars to extract .so files from
configurations { natives }

def setVersionName = { ->
    if (project.hasProperty("androidVersionName")) {
        println("Replacing versionName with supplied build parameter: " +
                "$androidVersionName")
        return androidVersionName
    } else {
        return "1.0"
    }
}

android {
    buildToolsVersion '28.0.3'
    ndkVersion '21.4.7075529' // current LTS atm

    compileSdkVersion 28
    defaultConfig {
        applicationId "com.nvidia.ar.hellocloudxr"

        // 24 is the minimum since ARCore only works with 24 and higher.
        // but as CloudXR library now uses 25 min, we need to match here.
        minSdkVersion 25
        targetSdkVersion 28
        versionCode 1
        versionName setVersionName()

        externalNativeBuild {
            cmake {
                cppFlags "-std=c++1y", "-Wall"
                arguments "-DANDROID_STL=c++_shared",
                        "-DANDROID_ABI=arm64-v8a",
                        "-DARCORE_LIBPATH=${arcore_libpath}/jni",
                        "-DARCORE_INCLUDE=${project.rootDir}/../../libraries/include",
                        "-DGLM_INCLUDE=${project.rootDir}/../../third_party/glm",
                        "-DOBOE_LIBPATH=${project.rootDir}/libs/Oboe/jni",
                        "-DOBOE_INCLUDE=${project.rootDir}/libs/Oboe/include",
                        "-DCLOUDXR_LIBPATH=${project.rootDir}/libs/CloudXR/jni",
                        "-DCLOUDXR_INCLUDE=${project.rootDir}/libs/CloudXR/include",
                        "-DC_SHARED_INCLUDE=${project.rootDir}/../../../../shared"
            }
        }
        ndk {
            abiFilters 'arm64-v8a'
        }
        sourceSets {
            main {
                jniLibs.srcDirs = ["${project.rootDir}/libs/Oboe/jni"]
            }
        }
    }

    signingConfigs {
        debug {
            storeFile file('./dummy.jks')
            storePassword 'nvidia'
            keyAlias 'nvidia'
            keyPassword 'nvidia'
        }
        release {
            storeFile file('./dummy.jks')
            storePassword 'nvidia'
            keyAlias 'nvidia'
            keyPassword 'nvidia'
        }
    }

    compileOptions {
        sourceCompatibility JavaVersion.VERSION_1_8
        targetCompatibility JavaVersion.VERSION_1_8
    }
    buildTypes {
        release {
            minifyEnabled false
            proguardFiles getDefaultProguardFile('proguard-android.txt'), 'proguard-rules.pro'
            signingConfig signingConfigs.release
        }
    }
    externalNativeBuild {
        cmake {
            path "CMakeLists.txt"
        }
    }
}

repositories {
    flatDir {
        dirs "${project.rootDir}/libs"
    }
}

dependencies {
    // ARCore library
    implementation 'com.google.ar:core:1.26.0'
    natives 'com.google.ar:core:1.26.0'

    implementation 'com.android.support:appcompat-v7:28.0.0'
    implementation 'com.android.support:design:28.0.0'
    implementation 'com.nvidia.CloudXRClient:CloudXR@aar'
}

// Extracts the shared libraries from aars in the natives configuration.
// This is done so that NDK builds can access these libraries.
task extractNativeLibraries() {
    // Always extract, this insures the native libs are updated if the version changes.
    outputs.upToDateWhen { false }
    doFirst {
        configurations.natives.files.each { f ->
            copy {
                from zipTree(f)
                into arcore_libpath
                include "jni/**/*"
            }
        }
    }
}

tasks.whenTaskAdded {
    task-> if (task.name.contains("external") && !task.name.contains("Clean")) {
        task.dependsOn(extractNativeLibraries)
    }
}

preBuild.dependsOn(':extractOboe')
preBuild.dependsOn(':extractCloudXR')
